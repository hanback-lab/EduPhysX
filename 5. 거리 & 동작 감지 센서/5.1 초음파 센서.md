# 초음파 센서
## 초음파 센서
초음파 센서는 40 kHz 대역의 초음파를 공기 중으로 발사(Trigger)한 뒤, 물체에 반사되어 돌아오는 시간을 측정하여 물체까지의 거리를 계산하는 센서입니다.
기본 원리는 다음과 같습니다.

- Trigger 핀에 짧은 펄스(10 μs)를 인가하면 송신부가 초음파를 발사합니다.
- 발사된 초음파가 물체에 부딪혀 반사되어 돌아오면 수신부가 이를 감지합니다.
- 이때 발생하는 Echo 신호의 High 구간 시간이 초음파가 이동한 왕복 시간에 해당합니다.
- 거리 = (왕복 시간 × 음속) ÷ 2 로 계산합니다.
    - 음속은 대기 중 약 340 m/s (온도·습도에 따라 변동)

즉, 측정된 시간을 거리로 환산하면 손쉽게 물체와의 거리를 얻을 수 있습니다.

장점
- 구조가 단순하고 가격이 저렴합니다.
- 비교적 정밀하게 근거리(2cm ~ 수 m 범위)의 거리를 측정할 수 있습니다.
- 아두이노, 라즈베리파이 등에서 라이브러리 없이도 제어가 가능합니다.

단점
- 초음파가 직진성이 강하기 때문에 각도가 있는 물체나 흡음 재질(천, 스펀지 등)에서는 반사가 약합니다.
- 여러 개를 동시에 사용하면 초음파 간섭(크로스토크)이 발생할 수 있습니다.
- 측정 가능한 최대 거리(약 4~5 m)와 최소 거리(약 2 cm)의 제한이 있습니다.

![Ultrasonic Schematic](res/Ultrasonic%20Schematic.png)

## 초음파 센서 연결 
초음파 센서 하드웨어 연결은 다음과 같습니다. 

| Pin | 연결 대상 | 설명 |
|:---:|:---:|:---|
| 5V | +5V | 전원 |
| GND | GND | 접지 |
| 9 | TRIG | Ultrasonic Trig |
| 8 | ECHO | Ultrasonic Echo |

## 초음파 센서 제어 
초음파 센서를 활용한 거리 측정 예제 입니다. Trigger 와 연결된 GPIO 를 제어하여 초음파를 발생시키고, 초음파가 반사되서 들어오는 시간을 Echo 와 연결된 GPIO 의 신호 변화를 감지하는 코드입니다. pulseIn() 은 GPIO의 입력 변화를 감지하는 함수로 인자로 전달된 신호가 다른 신호로 변화될때 까지의 시간을 반환합니다. 

만약 pulseIn(9,HIGH) 로 코드를 작성했다면, 9번 GPIO의 신호가 HIGH 에서 LOW 로 변화할떄까지 대기했다가 변화한 순간까지의 마이크로초를 반환합니다. 

```cpp
const int PIN_TRIG = 9;
const int PIN_ECHO = 8;

long duration;
float distance;

void setup() {
  Serial.begin(115200);
  pinMode(PIN_TRIG, OUTPUT);
  pinMode(PIN_ECHO, INPUT);
}

void loop() {
  digitalWrite(PIN_TRIG, LOW);
  delayMicroseconds(2);
  digitalWrite(PIN_TRIG, HIGH);
  delayMicroseconds(10);
  digitalWrite(PIN_TRIG, LOW);

  duration = pulseIn(PIN_ECHO, HIGH);
  distance = (duration * 0.034) / 2.0;

  Serial.print("Distance: ");
  Serial.print(distance);
  Serial.println(" cm");

  delay(200);
}
```

## Serial Plotter 활용
Serial Plotter 를 통해 초음파 센서값을 확인하면 다음과 같이 작성할 수 있습니다. 거리 값이외에 zero 를 출력하는 이유는 0cm 기준을 표기하기 위함입니다. 

```cpp
const int PIN_TRIG = 9;
const int PIN_ECHO = 8;

long duration;
float distance;
int zero = 0;

void setup() {
  Serial.begin(115200);
  pinMode(PIN_TRIG, OUTPUT);
  pinMode(PIN_ECHO, INPUT);
}

void loop() {
  digitalWrite(PIN_TRIG, LOW);
  delayMicroseconds(2);
  digitalWrite(PIN_TRIG, HIGH);
  delayMicroseconds(10);
  digitalWrite(PIN_TRIG, LOW);

  duration = pulseIn(PIN_ECHO, HIGH);
  distance = (duration * 0.034) / 2.0;

  Serial.print("Variable_1:");
  Serial.print(distance);
  Serial.print(",");
  Serial.print("Variable_2:");
  Serial.println(zero);
  
  delay(10);
}
```

## FND를 활용한 시각화
측정한 거리 값을 FND 에 표기하는 예제입니다. 거리값은 소수점 1자리까지만 표현합니다. 측정 시간과 출력시간을 조절하며 알맞는 동작 주기를 찾아보세요.

```cpp
#include <Wire.h>

const int PIN_TRIG = 9;
const int PIN_ECHO = 8;
const unsigned long ECHO_TIMEOUT_US = 25000UL;

#define HT16K33 0x70

uint8_t digitMap[10] = {
  0b00111111, 
  0b00000110, 
  0b01011011, 
  0b01001111, 
  0b01100110, 
  0b01101101, 
  0b01111101, 
  0b00000111, 
  0b01111111, 
  0b01101111
};

void ht16k33_write(uint8_t addr, uint8_t data) {
  Wire.beginTransmission(HT16K33);
  Wire.write(addr);
  Wire.write(data);
  Wire.endTransmission();
}

void displayDigit(int pos, int num, bool dot) {
  uint8_t addr = pos * 2;
  uint8_t data = digitMap[num];
  if(dot)
    data |= 1<<7;
  ht16k33_write(addr, data);
}

void showCM(float d) {
  int scaled = (int)round(d * 10.0f);
  int thou = (scaled / 1000) % 10;
  int hund = (scaled / 100)  % 10;
  int tens = (scaled / 10)   % 10;
  int ones = (scaled)        % 10;

  displayDigit(0, thou, false);
  displayDigit(1, hund, false);
  displayDigit(2, tens, true);
  displayDigit(3, ones, false);
}

float measureDistanceCm() {
  digitalWrite(PIN_TRIG, LOW);
  delayMicroseconds(2);
  digitalWrite(PIN_TRIG, HIGH);
  delayMicroseconds(10);
  digitalWrite(PIN_TRIG, LOW);

  unsigned long dur = pulseIn(PIN_ECHO, HIGH);
  float cm = (dur * 0.0343f) / 2.0f;
  if (cm > 400)
    cm = 400;
  return cm;
}

void setup() {
  Serial.begin(115200);
  pinMode(PIN_TRIG, OUTPUT);
  pinMode(PIN_ECHO, INPUT);
  Wire.begin();
  Wire.beginTransmission(HT16K33);
  Wire.write(0x21);
  Wire.endTransmission();
  Wire.beginTransmission(HT16K33);
  Wire.write(0x81);
  Wire.endTransmission();
  Wire.beginTransmission(HT16K33);
  Wire.write(0xE0 | 0x0F);
  Wire.endTransmission();
}

void loop() {
  float d = measureDistanceCm();
  Serial.println(d);
  showCM(d);
  delay(100);
}
```