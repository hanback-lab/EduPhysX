# 근접 센서
## 근접 센서
근접센서(Proximity Sensor) 는 물체가 가까이 다가오는 것을 비접촉 방식으로 감지하는 센서입니다.
그중 LJ12A3는 금속 물체에 반응하는 유도형(Inductive) 센서로, 금속이 센서 앞에 접근했을 때 신호를 출력합니다.

설치된 센서는 평상시 일정한 자기장을 형성하고 있습니다. 금속 물체가 센서 앞에 오면 자기장이 흐트러지고, 이 변화를 감지하여 출력 핀(OUT 핀)에서 디지털 신호(High/Low)를 발생시킵니다. 따라서 마이크로컨트롤러(MCU)나 임베디드 장치에서 금속 감지 여부를 쉽게 확인할 수 있습니다.

### 근접센서의 동작 원리
- 코일(Induction Coil) : 센서 내부에서 자기장을 형성합니다.
- 금속 감지 : 금속이 접근하면 와전류가 발생하여 자기장이 약해집니다.
- 신호 처리 회로 : 자기장의 변화를 전기 신호로 변환합니다.
- 출력 핀(OUT 핀) : 감지 결과를 High/Low 신호로 외부 장치에 전달합니다.

### 근접센서의 특징
- 비접촉 감지 : 기계적 마모가 없어 긴 수명을 가집니다.
- 금속 전용 : 철, 알루미늄, 구리 등 금속 물체만 감지할 수 있습니다.
- 빠른 반응 속도 : 물체 접근 시 즉각적으로 출력 신호를 제공합니다.

![Proximity Schematic](res/Proximity%20Schematic.png)

## 근접 센서 연결 
| Pin | 연결 대상 | 설명 |
|:---:|:---:|:---|
| 5V | +5V | 전원 |
| GND | GND | 접지 |
| 2 | A | Proximity Sensor |

## 근접 센서 제어 

```cpp
const int prox_pin = 2;

void setup() {
    pinMode(prox_pin, INPUT_PULLUP); 
    Serial.begin(115200);
}

void loop() {
    bool detected = (digitalRead(prox_pin) == LOW); 

    static unsigned long t = 0;
    if (millis() - t > 500) {
        Serial.println(detected ? "Detect" : "Not Detect");
        t = millis();
    }
}
```

```cpp
const int prox_pin = 2;
volatile unsigned long count = 0; 

void onFalling() { 
    count++;
}

void setup(){
    pinMode(prox_pin, INPUT_PULLUP);
    attachInterrupt(digitalPinToInterrupt(prox_pin), onFalling, FALLING);
    Serial.begin(115200);
}

void loop(){
    static unsigned long last = 0;
    if (millis() - last > 1000) {
        noInterrupts();
        unsigned long c = count;
        count = 0;
        interrupts();
        Serial.print("Detecting Count: ");
        Serial.println(c);
        last = millis();
    }
}
```