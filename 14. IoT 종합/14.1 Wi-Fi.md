# Wi-Fi
## Wi-Fi란 무엇인가?
Wi-Fi(Wireless Fidelity)는 전선을 사용하지 않고 무선 전파를 이용해 데이터를 송수신하는 통신 기술입니다. 주로 2.4GHz 또는 5GHz 대역에서 동작하며, 스마트폰·노트북·IoT 기기 등 대부분의 현대 전자기기가 이 기술을 통해 네트워크에 연결됩니다.

IoT(사물 인터넷) 분야에서 Wi-Fi는 다음과 같은 이유로 널리 사용됩니다:
- 케이블 없이 어디서나 연결 가능
  - 배선이 필요 없어 편리하며, 센서·장치 설치가 자유롭습니다.
- 인터넷 접속이 쉬움
  - 스마트홈, 모니터링 시스템, 웹 기반 제어 시스템에서 인터넷을 통한 원격 제어가 매우 간단합니다.
- 네트워크 통합에 유리
  - 스마트폰·PC·라즈베리파이 등 기존 네트워크 환경과 쉽게 통합할 수 있습니다.
- 비교적 높은 전송 속도
  - Bluetooth나 LoRa 같은 저속 무선보다 빠르고, ESP/WizFi 계열처럼 비교적 강력한 모듈을 이용하면 간단한 웹 서버까지 구동 가능합니다.

## 아두이노에서 Wi-Fi를 사용하는 방법
아두이노 UNO나 Mega2560 같은 기본 보드는 독자적으로 Wi-Fi 기능을 갖고 있지 않습니다. 따라서 별도의 무선 모듈을 연결해야 하며, 대표적인 방식은 다음과 같습니다:

1. UART 기반 Wi-Fi 모듈 (WizFi360, ESP8266 등)
  - AT Command 방식
  - 아두이노 ↔ 모듈 간 UART로 통신
  - 아두이노는 “명령 전송”, 모듈은 “Wi-Fi 기능 수행” 역할
  - 전력 소모와 동작 안정성이 좋아 교육 및 프로젝트에 널리 사용됨
2. 아두이노 Wi-Fi Shield
  - SPI 통신 기반 확장 보드
  - 상단에 바로 결합하여 사용

여기서는 UART 기반의 Wi-Fi 모듈을 활용합니다. 

## WizFi360 아두이노 라이브러리 설치 
WizFi360 모듈을 사용하려면 Arduino IDE에 라이브러리를 직접 설치해야 합니다.
이 라이브러리는 기본 라이브러리 매니저에서 검색되지 않으므로, GitHub 저장소에서 ZIP 파일을 다운로드하여 수동으로 추가합니다.
1. GitHub 저장소에서 ZIP 다운로드
    - [WizFi360 Arduino Library](https://github.com/Wiznet/WizFi360_arduino_library)
2. Arduino IDE → Sketch → Include Library → Add .ZIP Library…
3. 다운로드한 ZIP 파일 선택
4. 설치 후 File → Examples → WizFi360 메뉴에서 예제 확인 가능

## Wi-Fi 하드웨어 연결 
| Pin | 연결 대상 | 설명 |
|:---:|:---:|:---|
| 5V | +5V | 전원 |
| GND | GND | 접지 |
| 18 | RX | UART RX |
| 19 | TX | UART TX |

## Wi-Fi 접속 
이 예제는 WizFi360을 이용하여 공유기(AP) 에 접속하고 현재 네트워크 정보(SSID, BSSID, RSSI, IP, MAC 주소)를 출력하는 기본 코드입니다.

```cpp
#include "WizFi360.h"

char ssid[] = "YOUR_SSID";
char pass[] = "PASSWORD";
int status = WL_IDLE_STATUS;

void setup() {
  Serial.begin(115200);
  Serial1.begin(115200);
  WiFi.init(&Serial1);
  if (WiFi.status() == WL_NO_SHIELD) {
    Serial.println("WiFi shield not present");
    while (true);
  }

  while ( status != WL_CONNECTED) {
    Serial.print("Attempting to connect to WPA SSID: ");
    Serial.println(ssid);
    status = WiFi.begin(ssid, pass);
  }

  Serial.println("You're connected to the network");
}

void loop() {
  Serial.println();
  printCurrentNet();
  printWifiData();
  
  delay(10000);
}

void printWifiData() {
  IPAddress ip = WiFi.localIP();
  Serial.print("IP Address: ");
  Serial.println(ip);

  byte mac[6];
  WiFi.macAddress(mac);
  char buf[20];
  sprintf(buf, "%02X:%02X:%02X:%02X:%02X:%02X", mac[5], mac[4], mac[3], mac[2], mac[1], mac[0]);
  Serial.print("MAC address: ");
  Serial.println(buf);
}

void printCurrentNet() {
  Serial.print("SSID: ");
  Serial.println(WiFi.SSID());

  byte bssid[6];
  WiFi.BSSID(bssid);
  char buf[20];
  sprintf(buf, "%02X:%02X:%02X:%02X:%02X:%02X", bssid[5], bssid[4], bssid[3], bssid[2], bssid[1], bssid[0]);
  Serial.print("BSSID: ");
  Serial.println(buf);

  long rssi = WiFi.RSSI();
  Serial.print("Signal strength (RSSI): ");
  Serial.println(rssi);
}
```

## Wi-Fi WebServer
WizFi360을 이용하면 아두이노에서 간단한 웹 서버(WebServer) 를 만들 수 있습니다.
브라우저에서 아두이노의 IP 주소로 접속하면 HTML 페이지를 보내고, 필요할 때마다 AJAX로 센서값(A0)을 요청하여 실시간 갱신할 수 있습니다.

```cpp
#include "WizFi360.h"

char ssid[] = "YOUR_SSID";
char pass[] = "PASSWORD";
int status = WL_IDLE_STATUS;

WiFiServer server(80);

void setup() {
  Serial.begin(115200);
  Serial1.begin(115200);
  WiFi.init(&Serial1);

  if (WiFi.status() == WL_NO_SHIELD) {
    Serial.println("WiFi shield not present");
    while (true);
  }

  while (status != WL_CONNECTED) {
    Serial.print("Attempting to connect to SSID: ");
    Serial.println(ssid);
    status = WiFi.begin(ssid, pass);
    delay(1000);
  }

  Serial.println("WiFi connected.");
  printWifiStatus();
  server.begin();
}

void loop() {
  WiFiClient client = server.available();
  if (client) {
    Serial.println("New client");

    String reqLine = "";
    bool gotRequestLine = false;
    bool currentLineIsBlank = false;
    unsigned long startTime = millis();

    while (client.connected() && (millis() - startTime < 2000)) {
      if (client.available()) {
        char c = client.read();
        if (c == '\n') {
          if (!gotRequestLine) {
            gotRequestLine = true;
          }

          if (currentLineIsBlank) {
            break;
          } else {
            currentLineIsBlank = true;
          }
        } else if (c == '\r') {
        } else {
          if (!gotRequestLine) {
            reqLine += c; 
          }
          currentLineIsBlank = false;
        }
      }
    }

    Serial.print("Request line: ");
    Serial.println(reqLine);

    if (reqLine.startsWith("GET /a0")) {
      sendA0Value(client);
    } else {
      sendMainPage(client);
    }

    delay(1);
    client.stop();
    Serial.println("Client disconnected");
  }
}

void sendMainPage(WiFiClient &client) {
  client.print(
    "HTTP/1.1 200 OK\r\n"
    "Content-Type: text/html\r\n"
    "Connection: close\r\n"
    "Cache-Control: no-cache\r\n"
    "\r\n"
  );

  client.print(
    "<!DOCTYPE HTML>\r\n"
    "<html>\r\n"
    "<head>\r\n"
    "<meta charset=\"utf-8\">\r\n"
    "<title>A0 Monitor</title>\r\n"
    "</head>\r\n"
    "<body>\r\n"
    "<h1>A0 Monitor</h1>\r\n"
    "<p>A0 Value: <span id=\"a0\">-</span></p>\r\n"
    "<script>\r\n"
    "function scheduleNext() {\r\n"
    "  setTimeout(updateA0, 1000);\r\n"
    "}\r\n"
    "\r\n"
    "function updateA0() {\r\n"
    "  var xhr = new XMLHttpRequest();\r\n"
    "  xhr.onreadystatechange = function() {\r\n"
    "    if (xhr.readyState === 4) {\r\n"
    "      if (xhr.status === 200) {\r\n"
    "        document.getElementById('a0').textContent = xhr.responseText;\r\n"
    "      }\r\n"
    "      scheduleNext();\r\n"
    "    }\r\n"
    "  };\r\n"
    "  xhr.open('GET', '/a0', true);\r\n"
    "  xhr.send();\r\n"
    "}\r\n"
    "\r\n"
    "window.onload = function() {\r\n"
    "  updateA0();\r\n"
    "};\r\n"
    "</script>\r\n"
    "</body>\r\n"
    "</html>\r\n"
  );
}

void sendA0Value(WiFiClient &client) {
  int value = analogRead(A0);

  client.print(
    "HTTP/1.1 200 OK\r\n"
    "Content-Type: text/plain\r\n"
    "Connection: close\r\n"
    "Cache-Control: no-cache\r\n"
    "\r\n"
  );
  client.print(value);

  Serial.print("A0 sent: ");
  Serial.println(value);
}

void printWifiStatus() {
  Serial.print("SSID: ");
  Serial.println(WiFi.SSID());

  IPAddress ip = WiFi.localIP();
  Serial.print("IP Address: ");
  Serial.println(ip);

  Serial.print("Open browser: http://");
  Serial.println(ip);
  Serial.println();
}
```