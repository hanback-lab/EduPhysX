# 소리 센서
## 소리 
소리(Sound)는 물체의 진동이나 기체의 흐름에 의해 발생하는 파동의 일종입니다.
진동하는 물체에서 발생하는 소리는 발음체의 진동에 의한 소리라고 하며, 공기의 흐름에 의해 발생하는 소리는 기류음이라고 합니다.

소리는 일반적으로 세기(강약), 높이(고저), 음색(맵시) 의 세 가지 요소로 구분됩니다.
이 중 소리의 세기는 파동의 진폭에 따라 결정되며, 진폭이 클수록 큰 소리이고, 진폭이 작을수록 작은 소리입니다.

사람이 들을 수 있는 주파수 범위를 가청주파수라고 하며, 대략 16Hz에서 20kHz 정도입니다.

사운드 센서는 이러한 소리의 세기를 전기적 신호로 변환하여 측정할 수 있는 장치입니다. 센서를 활용하면 소리의 크기를 수치화할 수 있으며, 이를 바탕으로 LED를 켜거나 경보기를 울리는 등 다양한 제어 실습에 응용할 수 있습니다.

![Sound]()

![Sound Schematic](res/Sound%20Schematic.png)

여기서 사용된 MIC-FQ-057은 일종의 일렉트릿 콘덴서 마이크로폰(Electret Condenser Microphone) 기반 사운드 센서 모듈입니다.

이 모듈은 소리를 전압 신호로 변환하고, **내장된 증폭회로(OP-AMP)** 를 통해 아두이노 등 MCU가 읽을 수 있는 아날로그 신호로 출력합니다.

## 소리 센서 동작 원리 
1. 소리 입력 (압력 변화)
    - 주변의 음압이 변하면 마이크 내부의 진동막이 흔들립니다.
    - 이때 정전 용량이 변화하면서 전류가 발생합니다.
2. 전기 신호 변환
    - 발생한 미세한 전류는 OP-AMP(Operational Amplifier)를 통해 증폭됩니다.
    - 이 증폭된 신호는 소리의 세기에 따라 0 ~ 5 V 사이의 전압으로 변환됩니다.
3. ADC 변환
    - MCU(예: Arduino)는 analogRead() 함수를 이용해 입력 전압을 0 ~ 1023(10-bit) 범위의 디지털 값으로 읽습니다. 즉, 값이 클수록 소리가 크다는 의미입니다.

## 소리 센서 연결 
Sound 하드웨어 연결은 다음과 같습니다. 
| Pin | 연결 대상 | 설명 |
|:---:|:---:|:---|
| 5V | +5V | 전원 |
| GND | GND | 접지 |
| A1 | A | Sound Sensor |

## 소리 감지 프로그램 
가장 기본적인 형태로, ADC 값을 읽어 소리 세기를 확인하는 코드입니다.

```arduino
int pin_sound = A1;

void setup() {
  Serial.begin(115200);
  pinMode(pin_sound, INPUT);
}

void loop() {
  Serial.print("ADC Data :");
  Serial.println(analogRead(pin_sound));
  delay(100);
}
```

## dB 변환 
소리의 세기를 보다 실감나게 표현하기 위해 RMS(Root Mean Square) 계산을 통해 dB로 변환할 수 있습니다.

### dB 
데시벨(dB)는 소음의 정도를 나타내는 수치로, 소음이 공기에 가하는 압력으로 나타납니다. 보통 나뭇잎이 흔들리는 소리는 20데시벨, 이야기소리는 40-50 데시벨 정도로 보고 소음으로 느끼는 소음 수준이 80데시벨 정도라고 합니다.

사운드 센서를 통해 측정된 전압값을 데시벨로 변환하기 위해서는 다음과 같은 수식을 활용합니다. 
- dB=ref_db+20×log10​(rms​/ref_rms)
    - rms : 현재 측정된 소리의 제곱평균제곱근 값 
    - ref_rms : 기준이 되는 RMS 값(초기 환경에서의 평균값)
    - ref_db : 기준 RMS를 몇 dB로 둘 것인지 설정한 값 (예: 45 dB)

### RMS를 활용하는 이유 
오디오나 소음처럼 진폭이 시간에 따라 흔들리는 신호는 순간값(peak)이 아닌 평균적인 에너지를 반영해야 합니다.

RMS는 신호의 제곱값의 평균에 루트를 취해 계산하며, 결과적으로 “시간 동안 평균적으로 전달되는 에너지”를 반영합니다. 따라서 RMS를 이용하면 소리의 크기를 안정적이고 실감 있게 수치화할 수 있습니다.

## dB 변환 예제 
아래 예제는 일정 시간 동안의 RMS를 측정하고, 기준값 대비 상대적인 dB를 계산하는 방식입니다.

```arduino
const int pin_sound = A1;
const unsigned sample_us = 200;
const int n = 400;
const float alpha = 1.0f / 512.0f;
const float cal_time_ms = 2000.0;

float ref_rms = 1.0f;
float ref_db  = 45.0f;
float dc_mean = 512.0f;
bool calibrated = false;

int readSound() {
    return analogRead(pin_sound);
}

float measureRMS() {
    unsigned long nextT = micros();
    double sumSq = 0.0;
    for (int i = 0; i < n; i++) {
        int raw = readSound();
        dc_mean += alpha * ((float)raw - dc_mean);

        float centered = (float)raw - dc_mean;
        sumSq += centered * centered;

        nextT += sample_us;
        while ((long)(micros() - nextT) < 0) {
        }
    }
    float rms = sqrt(sumSq / (double)n);
    return rms;
}

void setup() {
    Serial.begin(115200);
    delay(200);

    for (int i=0; i<200; i++) { 
        dc_mean += alpha * ((float)readSound() - dc_mean); 
        delay(2); 
    }

    unsigned long t0 = millis();
    double acc = 0.0; int cnt = 0;
    while (millis() - t0 < cal_time_ms) {
        acc += measureRMS(); cnt++;
    }
    if (cnt > 0) 
        ref_rms = acc / (double)cnt;
    calibrated = true;

    Serial.println(F("Relative dB meter ready."));
    Serial.print(F("ref_rms=")); Serial.print(ref_rms, 4);
    Serial.print(F(", ref_db=")); Serial.println(ref_db, 1);
    Serial.println(F("Use Serial Plotter. (Tools > Serial Plotter)"));
}

void loop() {
  float rms = measureRMS();
  float eps = 1e-3;
  float db = ref_db + 20.0f * log10(max(rms, eps) / max(ref_rms, eps));

  Serial.print(db, 2);
  Serial.print('\t');
  Serial.print(rms, 2);
  Serial.print('\t');
  Serial.println(dc_mean, 1);
  delay(10);
}
```