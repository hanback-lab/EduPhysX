# 조도 센서
## 아날로그 입력과 ADC 
센서에서 출력되는 값은 대부분 전압(아날로그 신호) 입니다. 마이크로컨트롤러(Arduino 등)는 아날로그 값을 직접 계산할 수 없기 때문에, ADC(Analog-to-Digital Converter) 라는 변환기를 이용하여 전압을 디지털 값으로 바꾸어 처리합니다.

### ADC (Analog-to-Digital Converter)
ADC는 아날로그 전압을 일정한 규칙에 따라 0~1023 사이의 디지털 값으로 변환하는 장치입니다.
Mega2560의 ADC 는 10bit ADC 이며, 전압이 높을수록 ADC 값도 커집니다.

ADC 의 동작 방식은 다음과 같습니다. 
1. 샘플링(Sampling)
    - 아날로그 신호를 특정 시간 간격으로 찍어서 값을 가져오는 과정입니다. 샘플링 속도가 빠를수록 신호를 더 정확하게 표현할 수 있습니다.
2. 양자화(Quantization)
    - 연속적인 전압을 1024개의 “칸” 중 가장 가까운 정수로 변환하는 과정입니다.
3. 디지털 코드 출력
    - 최종적으로 MCU는 0~1023의 정수 값만 읽습니다. 이 값은 곧 센서의 출력 전압을 의미합니다.

## 조도 센서 
조도 센서(Light Sensor)는 주변의 빛의 세기(밝기) 를 전기적인 신호로 변환하는 센서입니다.
일반적으로 빛의 세기가 강할수록 출력 전압이 높아지고, 빛이 약할수록 출력 전압이 낮아지는 특성을 가집니다.

여기서 사용하는 조도 센서는 아날로그 출력 타입으로, 빛의 세기에 따라 전압이 선형적으로 변합니다.

이 센서는 내부에 광다이오드(Photo Diode) 와 증폭 회로를 포함하고 있어, 미세한 빛 변화도 안정적으로 감지할 수 있습니다.

![Light]()

모듈의 회로도는 다음과 같습니다. 

<img src="res/Light Schematic.png" style="width:40%;">

## 조도 센서 연결 
조도 센서 하드웨어 연결은 다음과 같습니다. 
| Pin | 연결 대상 | 설명 |
|:---:|:---:|:---|
| 5V | +5V | 전원 |
| GND | GND | 접지 |
| A0 | A | Light Sensor |

## 조도 센서 제어 프로그램 

```cpp
int pin_light = A0;

void setup() {
  Serial.begin(115200);
  pinMode(pin_light, INPUT);
}

void loop() {
  Serial.print("ADC Data :");
  Serial.println(analogRead(pin_light));
  delay(500);
}
```

## Serial Plotter 활용 
센서값의 변화는 빠르게 변동하므로, Arduino IDE의 Serial Plotter를 이용하면 시각적으로 변화를 볼 수 있습니다. 또한, EMA(Exponential Moving Average) 필터를 적용하여 노이즈를 줄인 데이터를 함께 표시할 수 있습니다.

Arduino IDE 의 Serial Plotter 에 대한 자세한 설명은 아래 링크를 참조하시기 바랍니다. 
- https://docs.arduino.cc/software/ide-v2/tutorials/ide-v2-serial-plotter/

### EMA(Exponential Moving Average) 필터 
조도 센서처럼 아날로그 신호를 측정하는 경우, 전원 노이즈나 조도 변화의 순간적인 흔들림 때문에 측정값이 계속 출렁이는 현상이 발생합니다.

이때 EMA(지수 이동 평균, Exponential Moving Average) 필터를 적용하면 갑작스러운 변화는 완화시키고, 전체적인 변화 추세는 유지할 수 있습니다.

- ema_new = ema_prev + α × (raw - ema_prev)
  - raw : 새로 측정된 센서 값 
  - ema_prev : 이전 단계의 ema 결과 
  - α : 필터 강도를 조절하는 가중치, 0~1사이의 값 

즉, 새로 들어온 측정값과 이전 평균값의 차이를 α만큼만 반영하여, 갑작스러운 변화는 완화하고 전체 추세를 부드럽게 따라가도록 하는 방식입니다.

여기서 α 의 값은 작을수록 반응은 느리지만 노이즈가 줄어드는 효과가 있습니다. 반대로 커지면 반응은 빠르지만 노이즈 제거효과는 값이 작을때 보다는 미비합니다. 

```cpp
int pin_light = A0;
float ema = 0.0;
float alpha = 0.2;

void setup() {
  Serial.begin(115200);
  pinMode(pin_light, INPUT);
}

void loop() {
  int raw = analogRead(pin_light);
  ema = ema + alpha * (raw - ema);
  Serial.print(raw); Serial.print(' ');
  Serial.println((int)ema);
  delay(20);
}
```

## 주변 밝기에 따른 LED 제어 
이번에는 주변 밝기에 따라 LED를 제어하는 프로그램을 작성해보겠습니다. 조도 센서 주변이 어두어진 상태에서는 LED를 더 밝게 켜고, 밝은 상태에서는 LED는 약하게 켜도록 구성하여 자동 조명 밝기 조절 시스템을 축소하여 구현합니다. 

| Pin | 연결 대상 | 설명 |
|:---:|:---:|:---|
| 5V | +5V | 전원 |
| GND | GND | 접지 |
| A0 | A | Light Sensor |
| 4 | D0 | LED 0 |

```cpp
int pin_light = A0;
int pin_led = 4;
float ema = 0.0;
float alpha = 0.2;

void setup() {
  pinMode(pin_led, OUTPUT);
  pinMode(pin_light, INPUT);
  Serial.begin(115200);
}

void loop() {
  int raw = analogRead(pin_light);
  ema = ema + alpha * (raw - ema);
  int value = 1023 - (int)ema;
  Serial.println(value);
  int pwm = map(value, 0, 1023, 0, 255);
  pwm = constrain(pwm, 0, 255);
  analogWrite(pin_led, pwm);

  delay(10);
}
```